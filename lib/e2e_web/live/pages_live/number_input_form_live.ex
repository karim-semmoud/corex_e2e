defmodule E2eWeb.NumberInputFormLive do
  use E2eWeb, :live_view

  import E2eWeb.DemoPage, only: [demo_page: 1, demo_section: 1]

  alias Corex.Toast
  alias E2e.Form.NumberInputForm
  alias E2eWeb.Demos.NumberInputDemo, as: Demo

  @phoenix_form_id "number-input-live-form-phoenix"
  @ecto_form_id "number-input-live-form-ecto"

  @impl true
  def mount(_params, _session, socket) do
    {:ok,
     socket
     |> assign(:page_title, "Number Input · Form")
     |> assign(:form_ecto, Demo.form_ecto())
     |> assign(:live_phoenix_heex, Demo.form_doc_live_phoenix_heex())
     |> assign(:live_phoenix_elixir, Demo.form_doc_live_phoenix_elixir())
     |> assign(:live_ecto_heex, Demo.form_doc_live_ecto_heex())
     |> assign(:live_ecto_elixir, Demo.form_doc_live_ecto_elixir())
     |> assign_forms()}
  end

  defp phoenix_value_from_submit(params, socket) do
    case params do
      %{"number_input_phoenix" => %{"value" => v}} when is_binary(v) -> v
      _ -> Phoenix.HTML.Form.normalize_value("number", socket.assigns.phoenix_form[:value].value)
    end
  end

  defp assign_forms(socket) do
    phoenix_form =
      Phoenix.Component.to_form(%{"value" => "1234"},
        as: :number_input_phoenix,
        id: @phoenix_form_id
      )

    ecto_form =
      %NumberInputForm{}
      |> NumberInputForm.changeset_validate(%{"value" => "1234"})
      |> Phoenix.Component.to_form(as: :number_input_ecto, id: @ecto_form_id)

    socket
    |> assign(:phoenix_form, phoenix_form)
    |> assign(:ecto_form, ecto_form)
  end

  @impl true
  def handle_event("change_phoenix", %{"number_input_phoenix" => params}, socket) do
    {:noreply,
     assign(
       socket,
       :phoenix_form,
       Phoenix.Component.to_form(params,
         as: :number_input_phoenix,
         id: @phoenix_form_id
       )
     )}
  end

  def handle_event("change_phoenix", _params, socket), do: {:noreply, socket}

  @impl true
  def handle_event("save_phoenix", params, socket) do
    value = phoenix_value_from_submit(params, socket)

    {:noreply,
     socket
     |> Toast.create("layout-toast", "Submitted", "value=#{inspect(value)}", :info,
       duration: 5000
     )
     |> assign(
       :phoenix_form,
       Phoenix.Component.to_form(%{"value" => value},
         as: :number_input_phoenix,
         id: @phoenix_form_id
       )
     )}
  end

  @impl true
  def handle_event("validate", %{"number_input_ecto" => params}, socket) do
    changeset =
      %NumberInputForm{}
      |> NumberInputForm.changeset_validate(params)
      |> Map.put(:action, :validate)

    {:noreply,
     assign(
       socket,
       :ecto_form,
       Phoenix.Component.to_form(changeset,
         action: :validate,
         as: :number_input_ecto,
         id: @ecto_form_id
       )
     )}
  end

  @impl true
  def handle_event("save", %{"number_input_ecto" => params}, socket) do
    case NumberInputForm.changeset_validate(%NumberInputForm{}, params) do
      %Ecto.Changeset{valid?: true} = changeset ->
        data = Ecto.Changeset.apply_changes(changeset)
        message = "Submitted: value=#{data.value}"

        {:noreply,
         socket
         |> Toast.create("layout-toast", "Submitted", message, :info, duration: 5000)
         |> assign(
           :ecto_form,
           Phoenix.Component.to_form(
             NumberInputForm.changeset_validate(%NumberInputForm{}, params),
             as: :number_input_ecto,
             id: @ecto_form_id
           )
         )}

      %Ecto.Changeset{} = changeset ->
        {:noreply,
         assign(
           socket,
           :ecto_form,
           Phoenix.Component.to_form(changeset,
             action: :insert,
             as: :number_input_ecto,
             id: @ecto_form_id
           )
         )}
    end
  end

  @impl true
  def render(assigns) do
    ~H"""
    <Layouts.app flash={@flash} mode={@mode} theme={@theme} path={@path}>
      <.demo_page path={@path} id="number-input-form-live-page" title={~t"Number Input · Form"}>
        <.demo_section
          id="number-input-live-form-phoenix-section"
          title={~t"Phoenix Form"}
          code_tabs={[
            %{value: "heex", label: ~t"Heex", language: :heex, code: @live_phoenix_heex},
            %{value: "elixir", label: ~t"Elixir", language: :elixir, code: @live_phoenix_elixir}
          ]}
        >
          <:preview>
            <Demo.form_preview_live_phoenix form={@phoenix_form} />
          </:preview>
        </.demo_section>

        <.demo_section
          id="number-input-live-form-ecto-section"
          title={~t"Phoenix Form + Ecto"}
          code_tabs={[
            %{value: "heex", label: ~t"Heex", language: :heex, code: @live_ecto_heex},
            %{value: "elixir", label: ~t"Elixir", language: :elixir, code: @live_ecto_elixir},
            %{value: "ecto", label: ~t"Ecto", language: :elixir, code: @form_ecto}
          ]}
        >
          <:preview>
            <Demo.form_preview_live_ecto form={@ecto_form} />
          </:preview>
        </.demo_section>
      </.demo_page>
    </Layouts.app>
    """
  end
end
