defmodule E2eWeb.MarqueeModel do
  use E2eWeb.Model, component: "marquee"

  import Wallaby.Query

  @anatomy_sections ~W(
    marquee-anatomy-minimal
    marquee-anatomy-custom-slots
    marquee-anatomy-with-images
  )

  def anatomy_section_ids, do: @anatomy_sections

  def valid_dom_id?(dom_id) do
    String.match?(dom_id, ~r/^[a-zA-Z0-9_-]+$/) and String.length(dom_id) > 0
  end

  def wait_host_marquee_ready(session, host_dom_id, opts \\ []) do
    if not valid_dom_id?(host_dom_id), do: raise(ArgumentError, "invalid host dom id")

    timeout = Keyword.get(opts, :timeout)

    q =
      css(~s|##{host_dom_id}[phx-hook="Marquee"]:not([data-loading])|, visible: :any)

    case timeout do
      nil -> assert_has(session, q)
      max_ms when is_integer(max_ms) and max_ms > 0 -> wait_for_has(session, q, timeout: max_ms)
    end

    session
  end

  def wait_section_marquee_ready(session, section_dom_id, opts \\ []) do
    if not valid_dom_id?(section_dom_id), do: raise(ArgumentError, "invalid section dom id")

    timeout = Keyword.get(opts, :timeout)

    q =
      css(
        ~s|section##{section_dom_id} [phx-hook="Marquee"]:not([data-loading])|,
        visible: :any
      )

    case timeout do
      nil -> assert_has(session, q)
      max_ms when is_integer(max_ms) and max_ms > 0 -> wait_for_has(session, q, timeout: max_ms)
    end

    session
  end

  defp marquee_root_selector(host_dom_id),
    do: ~s|[id="marquee:#{host_dom_id}"]|

  def host_paused?(session, host_dom_id) do
    has?(
      session,
      css("#{marquee_root_selector(host_dom_id)}[data-paused]", visible: :any)
    )
  end

  def wait_host_paused(session, host_dom_id, opts \\ []) do
    wait_for_has(
      session,
      css("#{marquee_root_selector(host_dom_id)}[data-paused]", visible: :any),
      opts
    )

    session
  end

  def wait_host_not_paused(session, host_dom_id, opts \\ []) do
    wait_for_has(
      session,
      css("#{marquee_root_selector(host_dom_id)}:not([data-paused])", visible: :any),
      opts
    )

    session
  end

  def click_in_section(session, section_id, button_label)
      when is_binary(section_id) and is_binary(button_label) do
    if String.contains?(button_label, "'") or String.contains?(button_label, "\"") do
      raise ArgumentError, "click_in_section/3 label must not include quotes"
    end

    click(
      session,
      xpath("(//*[@id=\'#{section_id}\']//button[normalize-space(.)=\'#{button_label}\'])[1]")
    )

    session
  end

  def pause_host(session, host_dom_id) when is_binary(host_dom_id) do
    if not valid_dom_id?(host_dom_id), do: raise(ArgumentError, "invalid host dom id")

    _ =
      execute_script(
        session,
        """
        const el = document.getElementById(arguments[0]);
        el?.dispatchEvent(new CustomEvent("corex:marquee:pause", { bubbles: false }));
        """,
        [host_dom_id]
      )

    session
  end

  def marquee_events_server_log_has_row?(session) do
    has?(
      session,
      css("#marquee-events-log-server tr[data-part='row']", visible: :any)
    )
  end

  def marquee_events_client_log_has_row?(session) do
    has?(
      session,
      css("#marquee-events-log-client tr[data-part='row']", visible: :any)
    )
  end
end
